# Maintainer: Tai Chi Minh Ralph Eastwood <tcmreastwood@gmail.com>

# Cardhu NVidia HardFP drivers, July 26th release
# Do not distribute these extensively or expect any support

buildarch=4

pkgname=('nvidia-grouper')
pkgver=16.3.0
pkgrel=3
pkgdesc="Nvidia drivers for the Nexus 7 (2012)"
arch=(armv7h)
license=('GPL')
url="http://github.com/raedwulf/aur"
source=('http://developer.nvidia.com/sites/default/files/akamai/mobile/files/L4T/cardhu_Tegra-Linux-R16.3.0_armhf.tbz2'
        'http://developer.nvidia.com/sites/default/files/akamai/mobile/files/L4T/cardhu_Tegra-Linux-codecs-R16.3.0_armhf.tbz2'
        'http://developer.nvidia.com/sites/default/files/akamai/mobile/files/L4T/cardhu_Tegra-Linux-tegra_drv_abi14-R16.3.0_armhf.tbz2'
        '01-grouper-input.conf'
        '10-grouper-screen.conf'
	'99-grouper-tegra.rules')

conflicts=('nvidia-trimslice')
options=('!distcc' '!ccache')

md5sums=('515563af14f8f06bd16daf864c6d888c'
         'd29c9fe932103b3386965a51532c9462'
         '3b61b46d94d71af19fa710716743ee33'
         '78cac585e78f77e570d78aaf47029579'
         'd896618ec708832b02b55a05a392315e'
         '9be8237a7f33b5479cba8ebd870ffd92')

package() {
  
  cd "${srcdir}"
  tar xf restricted_codecs.tbz2
  tar xf tegra_drv_abi_14.tbz2
  tar xf Linux_for_Tegra/nv_tegra/nvidia_drivers.tbz2

  mkdir -p "${pkgdir}"/opt/nvidia/
  mkdir -p "${pkgdir}"/usr/lib/firmware

  cp -a usr "${pkgdir}"/
  cp -a lib/firmware "${pkgdir}"/usr/lib/firmware
  mv "${pkgdir}"/usr/lib/lib* "${pkgdir}"/opt/nvidia/

  cd "${pkgdir}"/usr/lib/xorg/modules/drivers/
  ln -sf tegra_drv.abi14.so tegra_drv.so
  cd "${srcdir}"

  mkdir -p "${pkgdir}"/etc/X11/xorg.conf.d
  cp 01-grouper-input.conf "${pkgdir}"/etc/X11/xorg.conf.d/
  cp 10-grouper-screen.conf "${pkgdir}"/etc/X11/xorg.conf.d/

  mkdir -p "${pkgdir}"/usr/bin
  mv "${pkgdir}"/usr/sbin/brcm_patchram_plus "${pkgdir}"/usr/bin
  rm -rf "${pkgdir}"/usr/sbin

  # Udev rules
  mkdir -p "${pkgdir}"/usr/lib/udev/rules.d
  cp "${srcdir}"/99-grouper-tegra.rules "${pkgdir}"/usr/lib/udev/rules.d/

  # Create lib links
  mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
 
  # ensure we can load libs
  echo "/opt/nvidia/" > "${pkgdir}"/etc/ld.so.conf.d/nvidia-grouper.conf
}
